message("CTEST_FULL_OUTPUT")

#
# Include CTest Ext module
#

include("@CTEST_EXT_ROOT_DIR@/ctest_ext.cmake")

#
# Initialize testing
#

include("@TEMPLATE_PROJECT_DIR@/CTestConfig.cmake") # To get CTEST_PROJECT_NAME

set(CTEST_DASHBOARD_ROOT     "@DASHBOARD_ROOT@")
set(CTEST_SOURCE_DIRECTORY   "@TEMPLATE_PROJECT_DIR@")

ctest_ext_init()

#
# Check supported targets and models
#

check_if_matches(CTEST_TARGET_SYSTEM    "^Linux" "^Darwin")
check_if_matches(CTEST_MODEL            "^Experimental$" "^Nightly$")

#
# Configure the testing model (set options, not specified by user, to default values)
#

set_ifndef(CTEST_UPDATE_CMAKE_CACHE             TRUE)
set_ifndef(CTEST_EMPTY_BINARY_DIRECTORY         FALSE)
set_ifndef(CTEST_WITH_TESTS                     TRUE)

if(CTEST_MODEL MATCHES "Nightly")
    if(CTEST_GCOVR_EXECUTABLE AND EXISTS "${CTEST_GCOVR_EXECUTABLE}")
        set_ifndef(CTEST_WITH_COVERAGE          TRUE)
        set_ifndef(CTEST_COVERAGE_TOOL          "GCOVR")
    endif()
    if(CTEST_MEMORYCHECK_COMMAND AND EXISTS "${CTEST_MEMORYCHECK_COMMAND}")
        set_ifndef(CTEST_WITH_DYNAMIC_ANALYSIS  TRUE)
        set_ifndef(CTEST_DYNAMIC_ANALYSIS_TOOL  "CDASH")
    endif()
else()
    set_ifndef(CTEST_WITH_COVERAGE              FALSE)
    set_ifndef(CTEST_WITH_DYNAMIC_ANALYSIS      FALSE)
endif()

set_ifndef(CTEST_WITH_SUBMIT                    FALSE)

#
# Configure cmake options
#

if(CTEST_UPDATE_CMAKE_CACHE)
    set_ifndef(CTEST_CMAKE_GENERATOR "Unix Makefiles")
    set_ifndef(CTEST_CONFIGURATION_TYPE "Debug")

    if(CTEST_WITH_COVERAGE OR CTEST_WITH_GCOVR OR CTEST_WITH_LCOV)
        add_cmake_cache_entry("CMAKE_CXX_FLAGS" TYPE "STRING" "--coverage")
    endif()
endif()

#
# Start testing
#

ctest_ext_start()

#
# Configure
#

ctest_ext_configure()

#
# Build
#

ctest_ext_build()

#
# Test
#

ctest_ext_test()

#
# Coverage
#

ctest_ext_coverage(
    GCOVR
        HTML FILTER ".*/main.cpp"
)

#
# MemCheck
#

ctest_ext_dynamic_analysis()

#
# Submit
#

ctest_ext_submit()
